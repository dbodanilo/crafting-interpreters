// unambiguous
program         -> declaration* EOF ;
declaration     -> varDecl
                 | statement ;
varDecl         -> "var" IDENTIFIER ( "=" expression )? ";" ;
statement       -> exprStmt
                 | printStmt
                 | block ;
block           -> "{" declaration* "}" ;
exprStmt        -> expression ";" ;
printStmt       -> "print" expression ";" ;

// expression      -> equality ; (before the addition of the comma operator)
// expression      -> sequence ; (before the addition of the ternary operator)
// expression      -> ternary ;  (before the addition of the asignment expression)

expression      -> assignment ;
assignment      -> IDENTIFIER "=" assignment
                 | ternary ;

ternary         -> sequence ( "?" ternary ":" ternary )*
sequence        -> equality ( "," equality )* ;

equality        -> comparison ( ( "!=" | "==" ) comparison )* ;
comparison      -> addition ( ( ">" | ">=" | "<" | "<=" ) addition )* ;
addition        -> multiplication ( ( "-" | "+" ) multiplication )* ;

// correct, but not optimal for how it will be parsed
// multiplication  -> multiplication ( "/" | "*" ) unary
//                  | unary ;

multiplication  -> unary ( ( "/" | "*" ) unary )* ;
unary           -> ( "!" | "-" ) unary
                 | primary ;
primary         -> "true" | "false" | "nil"
                 | NUMBER | STRING
                 | "(" expression ")"
                 | IDENTIFIER ;

// ambiguous
expression -> literal
            | unary
            | binary
            | grouping ;
literal  -> NUMBER | STRING | "true" | "false" | "nil" ;
grouping -> "(" expression ")" ;
unary    -> ( "-" | "!" ) expression ;
binary   -> expression operator expression ;
operator -> "==" | "!=" | "<" | "<=" | ">" | ">="
          | "+"  | "-"  | "*" | "/"  ;
